/* REXX */
/**********************************************************************/
/* this routine gets software version information - version 1.0       */
/**********************************************************************/
/*------------------ initialize global variables ---------------------*/
trace 0
arg optn
address tso                                    /* set mode            */
numeric digits 30                              /* set decimal size    */
sysn = mvsvar('SYSNAME')                       /* get sysname         */
red   = '01'x                                  /* set colour          */
blue  = '02'x                                  /* attributes fo the   */
green = '03'x                                  /* dynamic display     */
white = '04'x                                  /* area                */
turq  = '05'x                                  /*                     */
pink  = '06'x                                  /*                     */
yellow = '07'x                                 /*                     */
title = ' Version Information for 'sysn        /* set display title   */
/*--------------------- get system variable information --------------*/
cvt = c2d(storage(10,4))                       /* point to cvt        */
ecvt = c2d(storage(d2x(cvt+140),4))            /* point to cvtecvt    */
osysd = mvsvar('sysopsys')                     /* get OS level        */
osys1 = substr(osysd,1,6)                      /* format              */
osys2 = substr(osysd,8,8)
osysv = osys1' 'osys2
fmid = substr(osysd,17,7)                      /* get FMID level      */
jesd = sysvar('sysjes')                        /* format              */
jesv = substr(jesd,9,8)
dfpv = substr(mvsvar('sysdfp'),4,8)
hsm  = sysvar('syshsm')                        /* get HSM level       */
hsml = right(substr(hsm,1,1),2,0)              /* format              */
hsmr = substr(hsm,2,2)
hsmm = right(substr(hsm,4,1),2,0)
hsmv = hsml'.'hsmr'.'hsmm
racf = sysvar('syslracf')                      /* get RACF level      */
racfl = right(substr(racf,1,1),2,0)            /* format              */
racfr = substr(racf,2,2)
racfm = right(substr(racf,4,1),2,0)
racfv = racfl'.'racfr'.'racfm
tso  = sysvar('systsoe')                       /* get TSO level       */
tsol = right(substr(tso,1,1),2,0)              /* format              */
tsor = substr(racf,2,2)
tsom = right(substr(tso,4,1),2,0)
tsov = tsol'.'tsor'.'tsom
smfid = mvsvar('syssmfid')                     /* get SMFID           */
splx = mvsvar('sysplex')                       /* get Sysplex Id.     */
mvs = mvsvar('sysmvs')                         /* get MVS level       */
/*---------------- get the vtam version and release ------------------*/
atcvt = c2d(storage(408,4))                    /* point to atcvt      */
vtamr = storage(d2x(atcvt+2),1)                /* get vtam version    */
vtaml = storage(d2x(atcvt+3),1)                /* get vtam release    */
vtamv = vtamr'.'vtaml                          /* build display       */
/*---------------- get dfa version------------------------------------*/
cvtdfa  = c2d(storage(d2x(cvt+1216),4))        /* get cvtdfa address  */
dfaprod = c2x(storage(d2x(cvtdfa+16),1))       /* product type        */
dfaver  = c2x(storage(d2x(cvtdfa+17),1))       /* version             */
dfarlse = c2x(storage(d2x(cvtdfa+18),1))       /* release             */
dfamod  = c2x(storage(d2x(cvtdfa+19),1))       /* modification        */

if dfamod = '' then dfamod = 0                 /* add modification    */

dfsms = dfaver'.'dfarlse'.'dfamod              /* build display       */

/*----------------------- get the ispf level -------------------------*/
address ispexec                                /* set mode            */
vget ZENVIR                                    /* get ispf version    */
ispfv = substr(ZENVIR,6,3)                     /* build display       */
drop ZENVIR                                    /* drop ispf variable  */

/*----------------------- display the information --------------------*/
address ispexec                                /* set mode            */
"addpop row(2) column(6)"                      /* start window        */

do until rc > 0
  "display panel(checkver)"                    /* display results     */
end

"rempop"                                       /* stop window         */
address tso                                    /* set mode            */

exit
