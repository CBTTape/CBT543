         TITLE 'GET UCB ADDRESS USING PROVIDED VOLSER'
GETUADDR CSECT
GETUADDR AMODE 31
GETUADDR RMODE ANY
         BAKR  R14,0                    save caller's ars + gprs
*                                       in the linkage stack
         LAE   R12,0(R15,0)             set up program base register
         USING GETUADDR,R12
*
GETPARMS EQU   *
         LM    R3,R4,0(R1)              load parm à
         USING RVOLUME,R3               get addressability
         USING DEVNUM,R4                get addressability r
*
GETWORK  EQU   *
         L     R9,=AL4(WORKALEN)        work area length
         STORAGE OBTAIN,LENGTH=(R9),ADDR=(R10),SP=0,KEY=8,             X
               LOC=BELOW,COND=NO,RELATED=(FREEWORK,'FREE WORK AREA')
         LAE   R13,0(R0,R1)             à the workarea
         USING SAVEAREA,R13             inform the assembler
*
MAINLINE EQU   *
         XC    UCBSWA,UCBSWA            clear the return area
         XC    UCBCS,UCBCS              clear the return area
         CLC   DNUM,=Cl4'0000'          are we looking for the volser?
         BE    GETUCB                   yes
         CLC   VOL,=Cl6'XXXXXX'         are we looking for the ucb?
         BE    GETVOLSR                 yes
         B     PARMERR                  something wrong
*
GETUCB   EQU   *
         UCBSCAN COPY,                                                 X
               VOLSER=VOL,                                             X
               DEVNCHAR=DNUM,                                          X
               DEVCLASS=DASD,                                          X
               DYNAMIC=YES,                                            X
               WORKAREA=UCBSWA,                                        X
               UCBAREA=UCBCS,                                          X
               RANGE=ALL,                                              X
               MF=(E,UCBPARM,COMPLETE)
         LTR   R15,R15                  test return code
         BNZ   ERROR                    exit if non-zero
         B     EXIT                     go home
*
GETVOLSR EQU   *
         XC    DEVNCHAR,DEVNCHAR        clear the return area
*
SCANLOOP EQU   *
         UCBSCAN COPY,                                                 X
               DEVCLASS=DASD,                                          X
               DEVNCHAR=DEVNCHAR,                                      X
               DYNAMIC=YES,                                            X
               WORKAREA=UCBSWA,                                        X
               UCBAREA=UCBCS,                                          X
               RANGE=ALL,                                              X
               MF=(E,UCBPARM,COMPLETE)
         LTR   R15,R15                  test return code
         BNZ   ERROR                    exit if non-zero
         CLC   DNUM,DEVNCHAR            is it the same?
         BNE   SCANLOOP                 keep looping if not
         LA    R9,UCBCS
         USING UCB,R9                   get addressability
         MVC   VOL,UCBVOLI              get volser
         B     EXIT                     go home
*
PARMERR  EQU   *
         WTO   'ERROR IN PARAMETER'
         B     EXIT                     go home
*
ERROR    EQU   *
         WTO   'ERROR IN SEARCHING FOR UCB'
         B     EXIT                     go home
*
EXIT     EQU   *
         LAE   R1,0(R0,R13)             address to free
         L     R9,=AL4(WORKALEN)        work area length
         STORAGE RELEASE,ADDR=(R1),LENGTH=(R9),SP=0,KEY=8,             X
               COND=NO,RELATED=(GETWORK,'OBTAIN WORK AREA')
         SLR   R15,R15                  set rc to zero
         PR                             restore & return
         LTORG
*
R0  EQU   0
R1  EQU   1
R2  EQU   2
R3  EQU   3
R4  EQU   4
R5  EQU   5
R6  EQU   6
R7  EQU   7
R8  EQU   8
R9  EQU   9
R10 EQU  10
R11 EQU  11
R12 EQU  12
R13 EQU  13
R14 EQU  14
R15 EQU  15
*
WORKAREA DSECT
SAVEAREA DS    CL72                     savearea
UCBSWA   DS    CL100                    ucbscan work area
UCBCS    DS    CL48                     ucb common segment
DEVNCHAR DS    CL4                      EBCDIC device number
         UCBSCAN MF=(L,UCBPARM)         List form of UCBSCAN
*
ENDWORK  DS    0D
WORKALEN EQU   *-WORKAREA               work area length
*
RVOLUME  DSECT
VOL      DS    CL6                      volume serial no
*
DEVNUM   DSECT
DNUM     DS    CL4                      device number
*
UCB      DSECT
         IEFUCBOB                       ucb macro id
         CVT   DSECT=YES,LIST=NO
         END   GETUADDR
